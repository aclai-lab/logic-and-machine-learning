{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "bcff4192",
   "metadata": {},
   "source": [
    "# Logic in Sole.jl"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bcb94fcf",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg\n",
    "Pkg.activate(\"..\")\n",
    "Pkg.instantiate()\n",
    "Pkg.update()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "91214ae4",
   "metadata": {},
   "source": [
    "## SoleLogics.jl"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9cf50690",
   "metadata": {},
   "source": [
    "`SoleLogics.jl` is not only the package in the `Sole.jl` framework specifically\n",
    "developed for logic: it is the core library of the framework itself.\n",
    "\n",
    "In a nutshell, it provides a fresh codebase for computational logic, featuring\n",
    "easy manipulation of:\n",
    "- Propositional and (multi)modal logics (atoms, logical constants, alphabets,\n",
    "grammars, crisp/fuzzy algebras);\n",
    "- Logical formulas (parsing, random generation, minimization);\n",
    "- Logical interpretations (propositional valuations, Kripke structures);\n",
    "- Algorithms for finite\n",
    "[model checking](https://en.wikipedia.org/wiki/Model_checking), that is,\n",
    "checking that a formula is satisfied by an interpretation.\n",
    "\n",
    "In this notebook, we will see examples of all these functionalities, providing\n",
    "a comprehensive overview of the whole package."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "99f089c2",
   "metadata": {},
   "outputs": [],
   "source": [
    "using SoleLogics"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7f44528d",
   "metadata": {},
   "source": [
    "### Propositional Logic"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "15f5d19e",
   "metadata": {},
   "source": [
    "Parsing and manipulating Formulas."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7f9551aa",
   "metadata": {},
   "outputs": [],
   "source": [
    "φ1 = parseformula(\"¬p∧q∨(s∨z)\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2c866cbb",
   "metadata": {},
   "outputs": [],
   "source": [
    "syntaxstring(φ1; parenthesize_commutatives = true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d21badb7",
   "metadata": {},
   "outputs": [],
   "source": [
    "filter(ψ -> height(ψ) == 1, subformulas(φ1))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "96f8b21f",
   "metadata": {},
   "outputs": [],
   "source": [
    "filter(ψ -> natoms(ψ) == 1, subformulas(φ1))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ea913c01",
   "metadata": {},
   "outputs": [],
   "source": [
    "φ2 = ⊥ ∨ Atom(\"t\") → φ1"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d41308b1",
   "metadata": {},
   "source": [
    "Generating random formulas."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7731111b",
   "metadata": {},
   "outputs": [],
   "source": [
    "alphabet = @atoms p q"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "04eb2fbf",
   "metadata": {},
   "outputs": [],
   "source": [
    "SoleLogics.BASE_PROPOSITIONAL_CONNECTIVES"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "735abd88",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Random\n",
    "\n",
    "h = 2;   # the height of the formula\n",
    "\n",
    "randformula(\n",
    "    Random.MersenneTwister(507),    # the random number generator we want to use\n",
    "    h,\n",
    "    alphabet,\n",
    "    SoleLogics.BASE_PROPOSITIONAL_CONNECTIVES\n",
    ")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "20079cb4",
   "metadata": {},
   "source": [
    "Model checking."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "14cb1ba3",
   "metadata": {},
   "outputs": [],
   "source": [
    "φ1 = parseformula(\"¬(p ∧ q)\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "cafb0c22",
   "metadata": {},
   "outputs": [],
   "source": [
    "I = TruthDict([\"p\" => true, \"q\" => false])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e837e6c1",
   "metadata": {},
   "outputs": [],
   "source": [
    "check(φ1, I)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b03a6a60",
   "metadata": {},
   "outputs": [],
   "source": [
    "φ2 = parseformula(\"¬(p ∧ q) ∧ (r ∨ q)\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "241e1603",
   "metadata": {},
   "outputs": [],
   "source": [
    "interpret(φ2, I)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bb8eb07b",
   "metadata": {},
   "source": [
    "### Modal Logic K"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d2256bba",
   "metadata": {},
   "source": [
    "Generating random formulas."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9c06f505",
   "metadata": {},
   "outputs": [],
   "source": [
    "SoleLogics.BASE_MODAL_CONNECTIVES"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "37afc239",
   "metadata": {},
   "outputs": [],
   "source": [
    "randformula(\n",
    "    Random.MersenneTwister(4267),\n",
    "    h,\n",
    "    alphabet,\n",
    "    SoleLogics.BASE_MODAL_CONNECTIVES\n",
    ")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "858f7104",
   "metadata": {},
   "source": [
    "Model checking."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7fb64183",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Graphs\n",
    "\n",
    "# Instantiate a Kripke frame with 5 worlds and 5 edges\n",
    "worlds = World.(1:5)\n",
    "edges = Edge.([(1,2), (1,3), (2,4), (3,4), (3,5)])\n",
    "fr = SimpleModalFrame(worlds, Graphs.SimpleDiGraph(edges))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1242e54a",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Enumerate the worlds that are accessible from the first world\n",
    "accessibles(fr, first(worlds))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "090f435a",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Assign each world a propositional interpretation\n",
    "@atoms p q\n",
    "valuation = Dict([\n",
    "    worlds[1] => TruthDict([p => true, q => false]),\n",
    "    worlds[2] => TruthDict([p => true, q => true]),\n",
    "    worlds[3] => TruthDict([p => true, q => false]),\n",
    "    worlds[4] => TruthDict([p => false, q => false]),\n",
    "    worlds[5] => TruthDict([p => false, q => true]),\n",
    "])\n",
    "\n",
    "# Instantiate a Kripke structure by combining a Kripke frame and the\n",
    "# propositional interpretations over each world\n",
    "K = KripkeStructure(fr, valuation)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "71d92d8f",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Generate a modal formula\n",
    "φ = parseformula(\"◊(p ∧ q)\");\n",
    "\n",
    "# Check the just generated formula on each world of the Kripke structure\n",
    "[w => check(φ, K, w) for w in worlds]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e4bf085d",
   "metadata": {},
   "source": [
    "### Temporal Modal Logics"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c4457278",
   "metadata": {},
   "source": [
    "Linear Temporal Logic (LTL)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d7419190",
   "metadata": {},
   "outputs": [],
   "source": [
    "# A frame consisting of 10 (evenly spaced) points\n",
    "fr = FullDimensionalFrame((10,), Point{1, Int64})\n",
    "allworlds(fr) |> collect"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6b47513f",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Linear Temporal Logic (LTL) `successor` relation\n",
    "accessibles(fr, Point(3), SoleLogics.SuccessorRel) |> collect"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9dfadb92",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Linear Temporal Logic (LTL) `greater than` (i.e., future) relation\n",
    "accessibles(fr, Point(3), SoleLogics.GreaterRel) |> collect"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "380fbfcb",
   "metadata": {},
   "source": [
    "Halpern and Shoham's Interval Temporal Logic (HS)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7a7d1c3e",
   "metadata": {},
   "outputs": [],
   "source": [
    "# An interval frame consisting of all intervals over 10 (evenly spaced) points\n",
    "fr = FullDimensionalFrame((10, ), Interval{Int64})\n",
    "allworlds(fr) |> collect"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d764fd59",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Interval Algebra (IA) relation `L` (later)\n",
    "accessibles(fr, Interval(3, 5), IA_L) |> collect"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e4d39e9a",
   "metadata": {},
   "source": [
    "### Fuzzy Logic"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "90460c4d",
   "metadata": {},
   "source": [
    "In standard fuzzy logics, instead of constraining ourselves to only `true` and\n",
    "`false` values, we let them be anythig between the continuous interval [0, 1].\n",
    "\n",
    "On the downside, we cannot use the classical evaluation for the propositional\n",
    "operators - what does it mean to be `0.3 and 0.5`?\n",
    "\n",
    "Fuzzy logics are defined over a `t-norm` operation, which will be our\n",
    "conjunction ($\\wedge$); we have 3 of them(*):\n",
    "- Goedel Logic, where the $tnorm(x, y)$ is defined as the $min\\{x, y\\}$\n",
    "- Lukasiewicz Logic, where the $tnorm(x, y)$ is defined as the\n",
    "$max\\{0, x+y-1\\}$\n",
    "- Product Logic, where the $tnorm(x, y)$ is defined as the arithmetic product\n",
    "$x \\cdot y$\n",
    "\n",
    "For each logic, the implication $x \\to y$ will be defined as the\n",
    "$max\\{z | tnorm(x, z) \\leq y\\}$.\n",
    "\n",
    "This can feel overwhelming at first, as it is very general: the important part\n",
    "is that we can derive the implication for each fuzzy logic from their `t-norm`.\n",
    "\n",
    "Finally, we will consider the disjunction between two values $x$ and $y$ simply\n",
    "as the $max\\{x, y\\}$ for all logics.\n",
    "\n",
    "(*) All other t-norms (hence, fuzzy logics) can be derived though a linear\n",
    "combination of these 3."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "50c6a0eb",
   "metadata": {},
   "outputs": [],
   "source": [
    "using SoleLogics.ManyValuedLogics   # fuzzy logics are defined in this submodule"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b21dab9f",
   "metadata": {},
   "outputs": [],
   "source": [
    "GodelLogic"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a855a012",
   "metadata": {},
   "outputs": [],
   "source": [
    "bot(GodelLogic)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "eeb7eba4",
   "metadata": {},
   "outputs": [],
   "source": [
    "top(GodelLogic)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "80835145",
   "metadata": {},
   "outputs": [],
   "source": [
    "uknown = ContinuousTruth(0.5)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5d9091a5",
   "metadata": {},
   "source": [
    "The $tnorm(x, y)$ for Goedel Logic is defined as the $min\\{x, y\\}$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "26401c00",
   "metadata": {},
   "outputs": [],
   "source": [
    "GodelLogic.tnorm(\n",
    "    uknown,\n",
    "    uknown\n",
    ")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "653a9eac",
   "metadata": {},
   "outputs": [],
   "source": [
    "LukasiewiczLogic"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8654761a",
   "metadata": {},
   "source": [
    "The $tnorm\\{x, y\\}$ for Lukasiewicz Logic is defined as the $max\\{0, x+y-1\\}."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3056942d",
   "metadata": {},
   "outputs": [],
   "source": [
    "LukasiewiczLogic.tnorm(\n",
    "    uknown,\n",
    "    uknown\n",
    ")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "924629f5",
   "metadata": {},
   "source": [
    "The $tnorm\\{x, y\\}$ for Product Logic is defined as the arithmetic product\n",
    "$x \\cdot y$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7473b4c8",
   "metadata": {},
   "outputs": [],
   "source": [
    "ProductLogic.tnorm(\n",
    "    uknown,\n",
    "    uknown\n",
    ")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "296c6340",
   "metadata": {},
   "source": [
    "### Many-Valued Logic"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "18d4cbbc",
   "metadata": {},
   "source": [
    "While in fuzzy logics we consider a total order between all the values,\n",
    "many-valued logics go even a step further: we also take into condiseration\n",
    "values which can also be non-comparable, i.e., partial orders.\n",
    "\n",
    "To do so, we leverage algebraic structures comprising lattices, such as Heyting\n",
    "Algebras."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1a8c8ef4",
   "metadata": {},
   "outputs": [],
   "source": [
    "using SoleLogics.ManyValuedLogics: G4, Ł4, H4\n",
    "using SoleLogics.ManyValuedLogics: α, β"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "905329e8",
   "metadata": {},
   "outputs": [],
   "source": [
    "getdomain(G4)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "87a5cbf2",
   "metadata": {},
   "outputs": [],
   "source": [
    "getdomain(Ł4)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5940941b",
   "metadata": {},
   "outputs": [],
   "source": [
    "getdomain(H4)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2df10573",
   "metadata": {},
   "outputs": [],
   "source": [
    "precedes(G4, α, β)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3e81bf56",
   "metadata": {},
   "outputs": [],
   "source": [
    "precedes(Ł4, α, β)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "29b1e0ac",
   "metadata": {},
   "outputs": [],
   "source": [
    "precedes(H4, α, β)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6bcc63a8",
   "metadata": {},
   "outputs": [],
   "source": [
    "precedes(H4, ⊥, α)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bb5da483",
   "metadata": {},
   "outputs": [],
   "source": [
    "precedes(H4, ⊥, β)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "42d9f84d",
   "metadata": {},
   "outputs": [],
   "source": [
    "precedes(H4, α, ⊤)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0bc05d91",
   "metadata": {},
   "outputs": [],
   "source": [
    "precedes(H4, β, ⊤)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d9828adf",
   "metadata": {},
   "source": [
    "While fuzzy logics differ on the `t-norm`, many-valued logics in general are\n",
    "defined, among other things (like the set of values), over a more general\n",
    "structure, called a `monoid`, that we will use to interpret conjunction\n",
    "($\\wedge$).\n",
    "\n",
    "This coincide with the `t-norm` for fuzzy logics."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ee8bda18",
   "metadata": {},
   "outputs": [],
   "source": [
    "G4.monoid(α, β)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5e61b362",
   "metadata": {},
   "outputs": [],
   "source": [
    "Ł4.monoid(α, β)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f06c45ca",
   "metadata": {},
   "source": [
    "Heyting algebras can be thought of as a generalization of Goedel algebras (resp.\n",
    "Goedel Logic) to partial orders: instead of taking the $min\\{x, y\\}$, which is\n",
    "not always possible, we take the $inf\\{x, y\\}$, .e., the greatest lower bound.\n",
    "\n",
    "For instance, since in our case $\\alpha$ and $\\beta$ are non-comparable, but\n",
    "both greater than $\\bot$ (and bigger than $\\top$), their $inf$ will be $\\bot$.\n",
    "\n",
    "An $inf$ and a $sup$ is always guaranteed to exist (and to be unique) by\n",
    "definition of a lattice."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d5fca0dc",
   "metadata": {},
   "outputs": [],
   "source": [
    "H4.monoid(α, β)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bba9decb",
   "metadata": {},
   "source": [
    "Let's have a quick look at a more comples example.\n",
    "\n",
    "The following is also an Heyting algebras, but with 9 values."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8c79932f",
   "metadata": {},
   "outputs": [],
   "source": [
    "using SoleLogics.ManyValuedLogics: H9\n",
    "using SoleLogics.ManyValuedLogics: ζ, η\n",
    "\n",
    "getdomain(H9)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ca75c045",
   "metadata": {},
   "source": [
    "Let's take, for example, values $\\zeta$ and $\\eta$: they are non-comparable,\n",
    "but they are both bigger than $\\bot, \\alpha, \\beta, \\delta$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "90b9bd98",
   "metadata": {},
   "outputs": [],
   "source": [
    "using SoleLogics.ManyValuedLogics: lesservalues\n",
    "\n",
    "lesservalues(H9, ζ)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6007f716",
   "metadata": {},
   "outputs": [],
   "source": [
    "lesservalues(H9, η)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "835c5d03",
   "metadata": {},
   "outputs": [],
   "source": [
    "intersect(lesservalues(H9, ζ), lesservalues(H9, η))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2876f5c2",
   "metadata": {},
   "source": [
    "Hence, the greatest of these values ($\\delta$) will be our result."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "71293bb2",
   "metadata": {},
   "outputs": [],
   "source": [
    "H9.monoid(ζ, η)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "401afea7",
   "metadata": {},
   "source": [
    "**Exercise**: try to do the same with other values from the H9 algebra, such as:\n",
    "- $\\zeta$ and $\\epsilon$\n",
    "- $\\zeta$ and $\\beta$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "37361716",
   "metadata": {},
   "source": [
    "Disjunction ($\\vee$) and implication ($\\to$) are generalized in a similar way:\n",
    "- to evaluate a disjunction $x \\vee y$, we will use the $sup\\{x, y\\}$ (lowest\n",
    "greater bound)\n",
    "- to evaluate an implication $x \\to y$, we will use the\n",
    "$sup\\{z | monoid(x, z) \\preceq y\\}$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4d300baa",
   "metadata": {},
   "source": [
    "## SoleReasoners.jl"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.8",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.8"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
